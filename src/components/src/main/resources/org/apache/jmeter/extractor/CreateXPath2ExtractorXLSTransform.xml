<!-- Licensed to the Apache Software Foundation (ASF) under one or more contributor
	license agreements. See the NOTICE file distributed with this work for additional
	information regarding copyright ownership. The ASF licenses this file to
	You under the Apache License, Version 2.0 (the "License"); you may not use
	this file except in compliance with the License. You may obtain a copy of
	the License at http://www.apache.org/licenses/LICENSE-2.0 Unless required
	by applicable law or agreed to in writing, software distributed under the
	License is distributed on an "AS IS" BASIS, WITHOUT WARRANTIES OR CONDITIONS
	OF ANY KIND, either express or implied. See the License for the specific
	language governing permissions and limitations under the License. -->
<!-- A slightly modified variant of: https://stackoverflow.com/questions/4746299/generate-get-xpath-from-xml-node-java -->
<xsl:stylesheet version="1.0"
	xmlns:xsl="http://www.w3.org/1999/XSL/Transform">
	<xsl:output omit-xml-declaration="yes" indent="yes" />
	<xsl:strip-space elements="*" />
	<xsl:variable name="vApos">'</xsl:variable>
	<xsl:template match="*[@* or not(*)] ">
		<xsl:if test="not(*)">
			<xsl:apply-templates select="ancestor-or-self::*"
				mode="path" />
			<xsl:value-of
				select="concat('/text()', '=',$vApos,.,$vApos)" />
			<xsl:text>&#xA;</xsl:text>
		</xsl:if>
		<xsl:apply-templates select="@*|*" />
	</xsl:template>
	<xsl:template match="*" mode="path">
		<xsl:value-of select="concat('/',name())" />
		<xsl:variable name="vnumPrecSiblings"
			select="count(preceding-sibling::*[name()=name(current())])" />
		<xsl:value-of
			select="concat('[', $vnumPrecSiblings +1, ']')" />
	</xsl:template>
	<xsl:template match="@*">
		<xsl:apply-templates
			select="../ancestor-or-self::*" mode="path" />
		<xsl:value-of
			select="concat('/@',name(), '=',$vApos,.,$vApos)" />
		<xsl:text>&#xA;</xsl:text>
	</xsl:template>
</xsl:stylesheet>
